# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

pkgbase=lib32-networkmanager
pkgname=(lib32-networkmanager lib32-libnm-glib)
pkgver=1.2.0
pkgrel=1
pkgdesc="Network Management daemon (32-bit)"
arch=(x86_64)
license=(GPL2 LGPL2.1)
url="http://www.gnome.org/projects/NetworkManager/"
_pppver=2.4.7
makedepends=(intltool dhclient iptables gobject-introspection gtk-doc
             "ppp=$_pppver" modemmanager lib32-dbus-glib iproute2 lib32-nss lib32-polkit
             wpa_supplicant lib32-libsoup lib32-systemd lib32-libgudev lib32-libmm-glib rp-pppoe
             lib32-libnewt lib32-libndp lib32-libteam vala perl-yaml python-gobject networkmanager)
checkdepends=(libx11 python-dbus)
source=(https://download.gnome.org/sources/NetworkManager/${pkgver:0:3}/NetworkManager-$pkgver.tar.xz
        hidepid.patch dhcpv6-mixed.patch
        NetworkManager.conf)
sha256sums=('e947cf30fa3d19dce88e6f6af51f06dc282b7db7996f946aaa37b03526ef2a80'
            '1de5b511b6b4a933739b0ef48ede1830fa3d6dea2277c1302b12b08fa83a73f1'
            'f7771790485f24d788fe35f5922ad044a29c6d89b34458d9e99938503b2a3b39'
            '452e4f77c1de92b1e08f6f58674a6c52a2b2d65b7deb0ba436e9afa91ee15103')

prepare() {
  cd NetworkManager-$pkgver

  # https://bugs.archlinux.org/task/48984
  patch -Np1 -i ../hidepid.patch

  # https://bugs.archlinux.org/task/49081
  patch -Np1 -i ../dhcpv6-mixed.patch

  2to3 -w libnm src tools

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export PATH="${srcdir}/path:$PATH"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  export LIBRARY_PATH="/usr/lib32:$LIBRARY_PATH"

  cd NetworkManager-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/bin/dhclient \
    --without-dhcpcd \
    --with-dnsmasq=/usr/bin/dnsmasq \
    --with-iptables=/usr/bin/iptables \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/bin/resolvconf \
    --with-pppd=/usr/bin/pppd \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --with-pppoe=/usr/bin/pppoe \
    --with-kernel-firmware-dir=/usr/lib/firmware \
    --with-session-tracking=systemd \
    --with-modem-manager-1 \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system \
    --disable-doc \
    --disable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

check() {
  cd NetworkManager-$pkgver
  make -k check
}

package_lib32-networkmanager() {
  depends=(lib32-libnm-glib iproute2 lib32-polkit wpa_supplicant lib32-libsoup lib32-libmm-glib
           lib32-libnewt lib32-libndp lib32-libteam networkmanager)

  cd NetworkManager-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C libnm uninstall
  make DESTDIR="$pkgdir" -C libnm-glib uninstall
  make DESTDIR="$pkgdir" -C libnm-util uninstall
  make DESTDIR="$pkgdir" -C vapi uninstall

  # Some stuff to move is left over
  mv "$pkgdir/usr/lib32/pkgconfig" ..

  # Remove conflicting files.
  rm -rf "${pkgdir}"/{etc,var,usr/{bin,lib,include,share}}
}

package_lib32-libnm-glib() {
  pkgdesc="NetworkManager library (32-bit)"
  depends=(lib32-libgudev lib32-nss lib32-dbus-glib lib32-util-linux)

  install -d "$pkgdir/usr/lib32"
  mv pkgconfig "$pkgdir/usr/lib32"

  cd NetworkManager-$pkgver
  make DESTDIR="$pkgdir" -C libnm install
  make DESTDIR="$pkgdir" -C libnm-util install
  make DESTDIR="$pkgdir" -C libnm-glib install
  make DESTDIR="$pkgdir" -C vapi install

  # Remove conflicting files.
  rm -rf "${pkgdir}"/{etc,usr/{bin,lib,include,share}}
}
