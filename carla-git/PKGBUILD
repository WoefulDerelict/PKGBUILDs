# Maintainer: Llewelyn Trahaearn <woefulderelict@gmail.com>
# Contributor: cocreature <moritz.kiefer<at>purelyfunctional<dot>org>

pkgname=carla-git
pkgver=1.9.7.r10.g45f74421
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=('i686' 'x86_64')
url="http://kxstudio.sf.net/carla"
license=('GPL2')
depends=('clthreads' 'clxclient' 'fftw' 'file' 'fluidsynth' 'glu' 'liblo' 'libpng12' 'linuxsampler' 'mxml' 'ntk-git' 'projectm' 'python' 'python-pyqt5' 'qt5-svg' 'zita-convolver' 'zita-resampler' 'zlib')
makedepends=('gtk2' 'gtk3' 'python-pyliblo' 'python-rdflib' 'qt4' 'zynaddsubfx')
optdepends=(
  'gtk2: LV2 GTK2 UI support'
  'gtk3: LV2 GTK3 UI support'
  'python-pyliblo: OSC support'
  'python-rdflib: LADSPA-RDF support'
  'qt4: LV2 QT4 UI support'
  'zynaddsubfx: ZynAddSubFX banks'
)
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname}"::'git://github.com/falkTX/Carla.git')
md5sums=('SKIP')
_branch=master

prepare() {
  cd "${pkgname}"
  git checkout ${_branch}
  git pull
}

pkgver() {
  cd "${pkgname}"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "${pkgname}"
  make DEFAULT_QT=5 EXPERIMENTAL_PLUGINS=true
}

package() {
  cd "${pkgname}"
  make DEFAULT_QT=5 EXPERIMENTAL_PLUGINS=true DESTDIR="${pkgdir}/" PREFIX=/usr install
}

