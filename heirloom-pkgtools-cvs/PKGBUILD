# Maintainer: Llewelyn Trahaearn <woefulderelict [at] gmail [dot] com>
# Contributor: aksr <aksr [at] t-com [dot] me>
# Contributor: Sascha Biermmanns <saschakb [at] gmail [dot] com>
# Contributor: Jens Staal <staal1978 [at] gmail [dot] com>

pkgname=heirloom-pkgtools-cvs
pkgver=2017.01.30
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="SVR4 application packaging tools."
url="http://heirloom.sourceforge.net/pkgtools.html"
license=('custom:"caldera"' 'custom:"opensolaris"')
depends=('heirloom-cvs' 'heirloom-devtools-cvs' 'heirloom-sh-cvs')
makedepends=('cvs')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('000-config.diff')
sha512sums=('c40c0776c71cf583f9940119158c6cbde74e29c5e733cb4eb58ebe70e75c651f7ee8def3af9435d0ecad4b8afe33632daac84e9361d23b816717e6bb67bc61cb')

_hmake() {
  env PATH="/opt/heirloom/bin:$PATH" MAKEFLAGS="" make "$@"
}

prepare() {
  cd ../
  cvs -d:pserver:anonymous:@heirloom.cvs.sourceforge.net:/cvsroot/heirloom login
  cvs -d:pserver:anonymous:@heirloom.cvs.sourceforge.net:/cvsroot/heirloom co -P heirloom-pkgtools
  cd ${srcdir}
  [ -d "${pkgname%-*}" ] && rm -rf "${pkgname%-*}"
  [ -d "${pkgname%-*}" ] || cp -a ../"${pkgname%-*}" "${pkgname%-*}"
  cd "${pkgname%-*}"
  patch -Np1 -i ${srcdir}/000-config.diff
}

pkgver() {
  date +%Y.%m.%d
}

build() {
  cd "${pkgname%-*}"
  make
}

package() {
  cd "${pkgname%-*}"
  make install ROOT="${pkgdir}"
  install -Dm0644 LICENSE/CALDERA.LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/CALDERA
  install -m0644 LICENSE/OPENSOLARIS.LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/OPENSOLARIS
}

