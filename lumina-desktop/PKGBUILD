# Maintainer: Chad "crossroads1112" Sharp <crossroads1112 at riseup dot net>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>

pkgname=lumina-desktop
pkgver=1.1.0
pkgrel=1
_dirname="${pkgname%-*}-${pkgver%p*}"
pkgdesc="A Lightweight QT5 Desktop for FreeBSD"
arch=('x86_64' 'i686')
url="https://lumina-desktop.org/"
license=('BSD')
depends=('fluxbox' 'oxygen' 'oxygen-icons' 'qt5-multimedia' 'xscreensaver')
makedepends=('lsb-release' 'qt5-tools')
optdepends=('acpi: backend for hardware monitoring.'
            'alsa-utils: backend for audio control.'
            'compton: Recommended compositor.'
            'fluxmod-styles: Theme elements to improve the appearence of window decorations.'
            'network-manager-applet: provides panel applet for managing network connections.'
            'numlockx: backend for manipulating numlock state.'
            'pavucontrol: Standalone mixer for PulseAudio.'
            'xorg-xbacklight: backend to adjust screen brightness.'
            'xterm: Terminal emulator.')
source=("https://github.com/trueos/lumina/archive/v${pkgver}.tar.gz")
sha512sums=('79b48e70fd7be3708966221c25025a8013a3ef96e00681056e99dfdd60809a3ff9d2048b3767d9fbc57c1c4f7e00fb36659cdca86fa41a322f125e9e061e88e0')

build() {
  cd $_dirname
  find ${srcdir}/${_dirname} -name *.desktop -exec sed -i 's/usr\/local/usr/' {} \;
  qmake-qt5 PREFIX="/usr" \
            QT5LIBDIR=/usr/lib/qt \
            QMAKE_CFLAGS_ISYSTEM= \
            QMAKE_CFLAGS_RELEASE="${CFLAGS}"\
            QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}"

  make
}

package() {
  cd $_dirname
  make INSTALL_ROOT="${pkgdir}" install
  mv "${pkgdir}"/usr/etc "${pkgdir}"/etc
  install -Dm0644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
