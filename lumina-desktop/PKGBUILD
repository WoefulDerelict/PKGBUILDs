# Maintainer: Chad "crossroads1112" Sharp <crossroads1112 at riseup dot net>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>

pkgname=lumina-desktop
pkgver=1.2.0
pkgrel=1
_dirname="${pkgname%-*}-${pkgver}"
pkgdesc="A Lightweight QT5 Desktop for FreeBSD"
arch=('x86_64' 'i686')
url="https://lumina-desktop.org/"
license=('BSD')
depends=('fluxbox' 'oxygen' 'oxygen-icons' 'qt5-multimedia' 'xscreensaver')
makedepends=('lsb-release' 'qt5-tools')
optdepends=('acpi: backend for hardware monitoring.'
            'alsa-utils: backend for audio control.'
            'compton: Recommended compositor.'
            'fluxmod-styles: Theme elements to improve the appearence of window decorations.'
            'network-manager-applet: provides panel applet for managing network connections.'
            'numlockx: backend for manipulating numlock state.'
            'pavucontrol: Standalone mixer for PulseAudio.'
            'xorg-xbacklight: backend to adjust screen brightness.'
            'xterm: Terminal emulator.')
source=("https://github.com/trueos/${pkgname%-*}/archive/v${pkgver}.tar.gz")
sha512sums=('c0843b638999efeebca3b7bc8bdf3187b1606909554fdca1e9158e49ff93cc13a2061bd2697add589d33eefe2bb3a662c5938161622d7fc796f693a61ec9bd40')

build() {
  cd $_dirname
  find ${srcdir}/${_dirname} -name *.desktop -exec sed -i 's/usr\/local/usr/' {} \;
  qmake-qt5 PREFIX="/usr" \
            QT5LIBDIR=/usr/lib/qt \
            QMAKE_CFLAGS_ISYSTEM= \
            QMAKE_CFLAGS_RELEASE="${CFLAGS}"\
            QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}"

  make
}

package() {
  cd $_dirname
  make INSTALL_ROOT="${pkgdir}" install
  mv "${pkgdir}"/usr/etc "${pkgdir}"/etc
  install -Dm0644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
