# Maintainer: Llewelyn Trahaearn <woefulderelict [at] gmail [dot] com>
# Contributor: aksr <aksr [at] t-com [dot] me>

pkgname=heirloom-mailx-cvs
pkgver=2017.03.28
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A mail user agent for Unix systems."
url="http://heirloom.sourceforge.net/mailx.html"
license=('custom')
depends=('heirloom-cvs' 'krb5' 'openssl>=1.1.0')
makedepends=('cvs')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('makefile.patch' 'man.diff' 'openssl.diff')
sha512sums=('dc999ad9a6c5e8da49a93dcff92aa76e6505f607bb5ee646c38e69fa7cdc23eb51b67666e9d0f1cda343ca031cb6425308315fefb9466d716ddb7d2470834d89'
            '2d82ec2a2798541ccc890884f209a56a6e7adad0ebbdc806428a5fceda8d260aeaf679cf7218f65d2bf47178006a6569c05879b66cf2ae80197db890b3f88793'
            '91862fb0d6a92ab1cb03c5aea73cf9cec875b59f555719d1d3064599d95411eb2c2d9f432d153ff2772384f659bafecce2c97c2569c83a30f3b31717fd80084d')

prepare() {
  cd ../
  cvs -d:pserver:anonymous:@nail.cvs.sourceforge.net:/cvsroot/nail login
  cvs -d:pserver:anonymous:@nail.cvs.sourceforge.net:/cvsroot/nail co nail
  cd ${srcdir}
  [ -d "${pkgname%-*}" ] && rm -rf "${pkgname%-*}"
  [ -d "${pkgname%-*}" ] || cp -a ../nail "${pkgname%-*}"
  cd "${pkgname%-*}"
  patch -Np1 -i ${srcdir}/openssl.diff
  patch -Np1 -i ${srcdir}/man.diff
  patch Makefile -i ${srcdir}/makefile.patch
}

pkgver() {
  date +%Y.%m.%d
}

build() {
  cd "${pkgname%-*}"
  make
}

package() {
  cd "${pkgname%-*}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${pkgname%-*}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

