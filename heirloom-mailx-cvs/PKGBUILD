# Maintainer: Llewelyn Trahaearn <woefulderelict [at] gmail [dot] com>
# Contributor: aksr <aksr [at] t-com [dot] me>

pkgname=heirloom-mailx-cvs
pkgver=2017.01.30
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A mail user agent for Unix systems."
url="http://heirloom.sourceforge.net/mailx.html"
license=('custom')
depends=('heirloom-cvs' 'krb5' 'openssl')
makedepends=('cvs')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('makefile.patch' 'man.diff' 'openssl.diff')
sha512sums=('dc999ad9a6c5e8da49a93dcff92aa76e6505f607bb5ee646c38e69fa7cdc23eb51b67666e9d0f1cda343ca031cb6425308315fefb9466d716ddb7d2470834d89'
            'f3f1846d1aae3e957d3d635589173722fd6966f35962d356e40e3a0595ceaa2145659a2dde478c79a3768ea110f4a28946b7e55cbc4c148a188583208b7ecc0a'
            '055e8dffce986d0070ad36c34448e18efe1d7ce9319724880527b7daca90b2f260a0fb90de1d206049651e4c7790fc692c3008fc2d08c9a71528e87be27f811a')

prepare() {
  cd ../
  cvs -d:pserver:anonymous:@nail.cvs.sourceforge.net:/cvsroot/nail login
  cvs -d:pserver:anonymous:@nail.cvs.sourceforge.net:/cvsroot/nail co nail
  cd ${srcdir}
  [ -d "${pkgname%-*}" ] && rm -rf "${pkgname%-*}"
  [ -d "${pkgname%-*}" ] || cp -a ../nail "${pkgname%-*}"
  cd "${pkgname%-*}"
  patch -Np1 -i ${srcdir}/openssl.diff
  patch -Np1 -i ${srcdir}/man.diff
  patch Makefile -i ${srcdir}/makefile.patch
}

pkgver() {
  date +%Y.%m.%d
}

build() {
  cd "${pkgname%-*}"
  make
}

package() {
  cd "${pkgname%-*}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${pkgname%-*}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

