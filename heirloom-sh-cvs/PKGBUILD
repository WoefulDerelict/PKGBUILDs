# Maintainer: aksr <aksr at t-com dot me>
# Contributor: Sascha Biermmanns <saschakb at gmail dot com>
# Contributor: Jens Staal <staal1978 at gmail dot com>
# Contributor: Daniel Leidisch <spam at leidisch dot net>

pkgname=heirloom-sh-cvs
pkgver=2016.05.21
pkgrel=1
pkgdesc="A portable variant of the traditional Unix shell, implements the SVR4/SVID3 level of the shell."
arch=('i686' 'x86_64')
url="http://heirloom.sourceforge.net/sh.html"
license=('custom:"caldera"' 'custom:"opensolaris"')
depends=('glibc')
makedepends=('cvs')
provides=("${pkgname%-*}" "sh")
conflicts=("${pkgname%-*}")
source=('000-config.diff')
sha512sums=('52417e869bc7c7646c8ef3d36a3376e86ea29e13ce03a521193d03db94fc6f9f7ccffae439d69212381ea47aec4cb96af1be3438571075911255ec3d5a304926')

prepare() {
  cd ../
  cvs -d:pserver:anonymous:@heirloom.cvs.sourceforge.net:/cvsroot/heirloom login
  cvs -d:pserver:anonymous:@heirloom.cvs.sourceforge.net:/cvsroot/heirloom co -P heirloom-sh
  cd ${srcdir}
  [ -d "${pkgname%-*}" ] && rm -rf "${pkgname%-*}"
  [ -d "${pkgname%-*}" ] || cp -a ../"${pkgname%-*}" "${pkgname%-*}"
  cd "${pkgname%-*}"
  patch -Np1 -i ${srcdir}/000-config.diff
}

pkgver() {
  date +%Y.%m.%d
}

build() {
  cd "${pkgname%-*}"
  make
}
package() {    
  cd "${pkgname%-*}"
  make ROOT="${pkgdir}" install
  install -Dm644 CALDERA.LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/CALDERA
  install -m644 OPENSOLARIS.LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/OPENSOLARIS
}

