# Maintainer: aksr <aksr at t-com dot me>
# Contributor: itikhonov
# Contributor: Sascha Biermmanns <saschakb at gmail dot com>
# Contributor: Jens Staal <staal1978 at gmail dot com>

pkgname=heirloom-doctools-cvs
pkgver=2016.05.14
pkgrel=1
pkgdesc="Utilities to format manual pages and other documents for output on terminals and printers."
arch=('i686' 'x86_64')
url="http://heirloom.sourceforge.net/doctools.html"
license=('custom:"berkeley"' 'custom:"caldera"' 'custom:"lucent"' 'custom:"opensolaris"' 'LGPL2.1' 'LPPL')
depends=('gcc-libs' 'heirloom-sh-cvs')
makedepends=('cvs')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=('config.diff')
sha512sums=('0898d6b3a137f2deb9c1c325165db399da73e193b469a0d8812337de7d7fdcd2581abedded7c9557c552a1970ae1badf7832ba335e69477ce7c66d03bcb023de')

pkgver() {
  date +%Y.%m.%d
}

prepare() {
  cd ../
  cvs -d:pserver:anonymous:@heirloom.cvs.sourceforge.net:/cvsroot/heirloom login
  cvs -d:pserver:anonymous:@heirloom.cvs.sourceforge.net:/cvsroot/heirloom co -P heirloom-doctools
  cd ${srcdir}
  [ -d "${pkgname%-*}" ] && rm -rf "${pkgname%-*}"
  [ -d "${pkgname%-*}" ] || cp -a ../"${pkgname%-*}" "${pkgname%-*}"
  cd "${pkgname%-*}"
  patch -Np1 -i ${srcdir}/config.diff
}

build() {
  cd "${pkgname%-*}"
  make
}

package() {
  cd "${pkgname%-*}"
  make ROOT="${pkgdir}" install
  install -Dm0644 LICENSE/BERKELEY.LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/BERKELEY
  install -m0644 LICENSE/CALDERA.LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/CALDERA
  install -m0644 LICENSE/LUCENT.LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LUCENT
  install -m0644 LICENSE/OPENSOLARIS.LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/OPENSOLARIS
}

