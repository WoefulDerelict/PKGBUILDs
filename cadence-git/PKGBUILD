# Maintainer: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: falkTX <falktx at gmail dot com>

pkgname=cadence-git
pkgver=r981.dea02f6
pkgrel=1
pkgdesc="JACK toolbox for audio production."
arch=('i686' 'x86_64')
url="http://kxstudio.sf.net/cadence"
license=('GPL2')
depends=('python-dbus' 'python-pyqt4' 'jack' 'qt4')
makedepends=('a2jmidid' 'jack_capture' 'ladish' 'pulseaudio-jack' 'python-rdflib' 'zita-ajbridge')
optdepends=(
  'a2jmidid: ALSA to JACK MIDI bridge'
  'jack_capture: recording via Cadence-Render'
  'ladish: session management via Claudia'
  'pulseaudio-jack: PulseAudio to JACK bridge'
  'python-rdflib: LADSPA-RDF support in Carla'
  'zita-ajbridge: ALSA to JACK bridge'
)
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
options=('!emptydirs')
install=${pkgname}.install
source=("${pkgname}::git+https://github.com/falkTX/Cadence.git")
sha512sums=('SKIP')
_branch=master

pkgver() {
  cd "${srcdir}/${pkgname}"
  git checkout ${_branch} --quiet
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${srcdir}/${pkgname}"
  git checkout ${_branch}
}

build() {
  cd "${srcdir}/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}
