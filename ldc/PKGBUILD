# $Id$
# Maintainer: Mihails Strasuns <public@dicebot.lv>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgbase=ldc
pkgname=("${pkgbase}" 'liblphobos')
groups=('dlang' 'dlang-ldc')
pkgver=1.1.0.beta3.r157.gbb3e1648
epoch=1
pkgrel=1
pkgdesc="D compiler based on the LLVM project including runtime and library."
arch=('i686' 'x86_64')
url="https://github.com/ldc-developers/ldc"
license=('BSD')
depends=('libconfig')
makedepends=('git' 'cmake' 'llvm' 'dmd')
#source=("git+$url#tag=v${pkgver}")
_commit=bb3e16481c14cf2bf161d9ff0e396500ab373ddb
source=("git+$url#tag=$_commit"
        "ldc2.conf"
       )
sha256sums=('SKIP'
            '2ef3b1090e25187305f18ce6fbbbc45527dcbb33570afbe30e177790813948db'
           )

pkgver() {
  cd ldc
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd ldc

  git submodule update --init --recursive

  [ -d install ] || mkdir install
  [ -d build ] || mkdir build
  cd build

  cmake \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_SKIP_RPATH=ON \
  -DINCLUDE_INSTALL_DIR=/usr/include/dlang/ldc \
  -DBUILD_SHARED_LIBS=ON \
  ..

  make -j 4
  make install DESTDIR=$srcdir/install
}

package_ldc() {
  pkgdesc="D compiler based on the LLVM project."
  depends=('liblphobos' 'libconfig')
  backup=("etc/ldc2.conf")
  provides=("d-compiler")

  cd install

  # binaries
  install -D -m755 ./usr/bin/ldmd2 $pkgdir/usr/bin/ldmd
  ln -s /usr/bin/ldmd $pkgdir/usr/bin/ldmd2
  install -D -m755 ./usr/bin/ldc2 $pkgdir/usr/bin/ldc
  ln -s /usr/bin/ldc $pkgdir/usr/bin/ldc2

  # default configuration files
  install -D -m644 $srcdir/ldc2.conf $pkgdir/etc/ldc2.conf

  # supplementaries
  install -D -m644 $srcdir/ldc/bash_completion.d/ldc2 $pkgdir/usr/share/bash-completion/completions/ldc

  # licenses
  install -D -m644 $srcdir/ldc/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE    
}

package_liblphobos() {
  pkgdesc="Runtime and Phobos library for the LLVM based D compiler."
  provides=("d-runtime" "d-stdlib")
  replaces=("liblphobos-devel")
  conflicts=("liblphobos-devel")
  depends=("curl")

  cd install

  # imports
  mkdir -p $pkgdir/usr/include/dlang
  cp -r ./usr/include/dlang/ldc $pkgdir/usr/include/dlang/ldc

  # libraries
  install -D -m644 ./usr/lib/libphobos2-ldc.so $pkgdir/usr/lib/liblphobos2.so
  install -D -m644 ./usr/lib/libdruntime-ldc.so $pkgdir/usr/lib/libldruntime.so
  install -D -m644 ./usr/lib/libphobos2-ldc-debug.so $pkgdir/usr/lib/liblphobos2-debug.so
  install -D -m644 ./usr/lib/libdruntime-ldc-debug.so $pkgdir/usr/lib/libldruntime-debug.so

  # licenses
  install -D -m644 $srcdir/ldc/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE    
}
