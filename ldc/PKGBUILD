# Maintainer: Llewelyn Trahaearn <woefulderelict [at] gmail [dot] com>
# Contributor: Mihails Strasuns <public@dicebot.lv>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgbase=ldc
pkgname=("${pkgbase}" 'liblphobos' 'lib32-liblphobos')
pkgver=1.1.1
pkgrel=1
epoch=1
pkgdesc="D compiler based on the LLVM project including runtime and library."
arch=('i686' 'x86_64')
url="https://github.com/ldc-developers/ldc"
license=('BSD')
groups=('dlang' 'dlang-ldc')
makedepends=('cmake' 'dmd' 'libconfig' 'lib32-curl' 'lib32-gcc-libs' 'llvm')
source=("$url/releases/download/v${pkgver}/ldc-${pkgver}-src.tar.gz"
        "ldc2.conf")
sha256sums=('3d35253a76288a78939fea467409462f0b87461ffb89550eb0d9958e59eb7e97'
            '2ef3b1090e25187305f18ce6fbbbc45527dcbb33570afbe30e177790813948db')

build() {
  cd ldc-$pkgver-src

  mkdir -p build && cd build

  cmake \
    -DMULTILIB=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DINCLUDE_INSTALL_DIR=/usr/include/dlang/ldc \
    -DBUILD_SHARED_LIBS=ON \
    ..

  make 
  make install DESTDIR=$srcdir/ldc-$pkgver-src/install
}

package_ldc() {
  pkgdesc="D compiler based on the LLVM project."
  depends=('liblphobos' 'libconfig' 'llvm-libs')
  provides=("d-compiler")
  backup=("etc/ldc2.conf")

  cd ldc-$pkgver-src/install

  # binaries
  install -D -m755 ./usr/bin/ldmd2 $pkgdir/usr/bin/ldmd
  ln -s /usr/bin/ldmd $pkgdir/usr/bin/ldmd2
  install -D -m755 ./usr/bin/ldc2 $pkgdir/usr/bin/ldc
  ln -s /usr/bin/ldc $pkgdir/usr/bin/ldc2

  # default configuration files
  install -D -m644 $srcdir/ldc2.conf $pkgdir/etc/ldc2.conf

  # supplementaries
  install -D -m644 ../bash_completion.d/ldc2 $pkgdir/usr/share/bash-completion/completions/ldc

  # licenses
  install -D -m644 ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE    
}

package_liblphobos() {
  pkgdesc="Runtime and Phobos library for the LLVM based D compiler."
  depends=("curl")
  provides=("d-runtime" "d-stdlib")
  conflicts=("liblphobos-devel")
  replaces=("liblphobos-devel")

  cd ldc-$pkgver-src/install

  # imports
  mkdir -p $pkgdir/usr/include/dlang
  cp -r ./usr/include/dlang/ldc $pkgdir/usr/include/dlang/ldc

  # libraries
  install -D -m644 ./usr/lib/libphobos2-ldc.so $pkgdir/usr/lib/liblphobos2.so
  install -D -m644 ./usr/lib/libdruntime-ldc.so $pkgdir/usr/lib/libldruntime.so
  install -D -m644 ./usr/lib/libphobos2-ldc-debug.so $pkgdir/usr/lib/liblphobos2-debug.so
  install -D -m644 ./usr/lib/libdruntime-ldc-debug.so $pkgdir/usr/lib/libldruntime-debug.so

  # licenses
  install -D -m644 ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE    
}

package_lib32-liblphobos() {
  pkgdesc="Runtime and Phobos library for the LLVM based D compiler. (32-bit)"
  arch=('x86_64')
  depends=('ldc' 'lib32-curl' 'lib32-gcc-libs')
  provides=("d-runtime" "d-stdlib")
  replaces=("lib32-liblphobos-devel")

  cd ldc-$pkgver-src/build

  # Libraries
  install -D -m644 ./lib32/libphobos2-ldc.so $pkgdir/usr/lib32/liblphobos2.so
  install -D -m644 ./lib32/libdruntime-ldc.so $pkgdir/usr/lib32/libldruntime.so
  install -D -m644 ./lib32/libphobos2-ldc-debug.so $pkgdir/usr/lib32/liblphobos2-debug.so
  install -D -m644 ./lib32/libdruntime-ldc-debug.so $pkgdir/usr/lib32/libldruntime-debug.so

  # License
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
