# Maintainer: Afri 5chdn <aur@cach.co>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=mist
pkgver=0.7.5
_pkgver=${pkgver//./-}
pkgrel=1
pkgdesc="The Ethereum browser."
arch=('i686' 'x86_64')
url="https://github.com/ethereum/mist"
license=('GPL3')
depends=('gconf'
         'gmp'
         'leveldb'
         'qt5-base'
         'qt5-declarative'
         'qt5-quickcontrols'
         'qt5-webengine'
         'readline')
makedepends=('gendesk' 'setconf')
optdepends=('ethereum: The cpp-ethereum commandline client.')
provides=("${pkgname}")
conflicts=("${pkgname}-git")
source=("${pkgname}.png")
sha512sums=('a5b3b675e713b6b7bf00008ec784495b268d8a18061d57f9a1a58a8f18cb1c0904b602d9d31b59d8b2be1c3afb92284bfb6fc5cc6c35650e5a0b3dffaa0211af')
sha512sums_i686=('b2b4dd24683c7e7c18e86ceeafc0e134b535789fef0bb4da4226bcbf5ad3b5330a197413e8c4d552b7862b4301eae87c092d7f855e591da04a353e471db6519a')
sha512sums_x86_64=('5bb06868639b63e568148b0636334859549eccfe1ecc4bc45278454e13effe21f0742d47697a08abd034e285e8ea60b50166f01ff78b4f728a439d712f5f2758')
source_i686=("${pkgname}-${_pkgver}-32.zip::https://github.com/ethereum/${pkgname}/releases/download/${pkgver}/Ethereum-Wallet-linux32-${_pkgver}.zip")
source_x86_64=("${pkgname}-${_pkgver}-64.zip::https://github.com/ethereum/${pkgname}/releases/download/${pkgver}/Ethereum-Wallet-linux64-${_pkgver}.zip")

package() {
  _arch="32"
  if [ "${CARCH}" = "x86_64" ]; then
    _arch="64"
  fi

  [ -f mist.desktop ] && rm mist.desktop
  gendesk $startdir/PKGBUILD
  # Set the name and path
  setconf "${pkgname}.desktop" Name "Mist Browser"
  setconf -a "${pkgname}.desktop" Path "/usr/lib/mist"

  install -d "${pkgdir}/usr/lib/${pkgname}"
  cp -a "${srcdir}/Ethereum-Wallet-linux${_arch}-${_pkgver}/." "${pkgdir}/usr/lib/${pkgname}"

  install -d "${pkgdir}/usr/share/pixmaps"
  install -Dm644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps"
  
  install -d "${pkgdir}/usr/share/applications"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${pkgname}/Ethereum-Wallet" "${pkgdir}/usr/bin/mist"
  ln -s "/usr/lib/${pkgname}/resources/node/geth/geth" "${pkgdir}/usr/bin/geth"

  install -d "${pkgdir}/usr/lib"
  ln -s "/usr/lib/${pkgname}/libnode.so" "${pkgdir}/usr/lib/libnode.so"

  ln -sf "/usr/lib/libgcrypt.so.11" "${pkgdir}/usr/lib/${pkgname}/libgcrypt.so.11"
  ln -sf "/usr/lib/libnotify.so.4" "${pkgdir}/usr/lib/${pkgname}/libnotify.so.4"

  rm "${pkgdir}/usr/lib/${pkgname}/LICENSE"

  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
  chmod 755 "${pkgdir}/usr/lib/${pkgname}/Ethereum-Wallet"
  chmod 755 "${pkgdir}/usr/lib/${pkgname}/libnode.so"
  chmod 755 "${pkgdir}/usr/lib/${pkgname}/resources/node/geth/geth"
}
